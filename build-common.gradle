// Common gradle configuration between offline and online build.
// This file can not be run by itself either run build.gradle or build-offline.gradle .

defaultTasks 'clean', 'assemble', 'test'

//    METADATA
// ==============
version = '1.1.0-SNAPSHOT'
description = 'Xenon: a middleware abstraction library that provides a simple programming interface to various compute and storage resources.'

// will generate a warning with JDK 8, since the runtime jar (rt.jar) of
// Java 7 is not available. Could configure gradle wrapper for this
sourceCompatibility = '1.7'
targetCompatibility = '1.7'

//  DEPENDENCIES
// ==============

//Get dependencies from Maven central repository, from JCenter and then locally
repositories {
    mavenCentral()
    jcenter()
    // TODO lib/test, could interfere with main sourceset. eg junit.jar in distribution
    flatDir { dirs 'lib', 'lib/cog-jglobus', 'lib/test' }
}

ext.cogJglobusVersion = '1.8.0'
ext.jschVersion = '0.1.50'
ext.apacheCommonsNetVersion = '3.3'

// PROJECT DIRECTORIES
// ==============
sourceSets {
    test {
        java {
            exclude 'nl/esciencecenter/xenon/integration/**'
            exclude '**/IT*.java'
            exclude '**/*IT.java'
            exclude 'nl/esciencecenter/xenon/adaptors/*/*AdaptorTest.java'
        }
    }
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            // TODO move all integration tests to src/integrationTest/java
            srcDirs 'src/test/java'
            include 'nl/esciencecenter/xenon/integration/**'
            include '**/IT*.java'
            include '**/*IT.java'
            include 'nl/esciencecenter/xenon/adaptors/*/*AdaptorTest.java'
        }
        // TODO move all integration resources to src/integrationTest/resources
        resources.srcDirs 'src/test/resources'
    }
    userguide {
        java {
            srcDir 'doc/userguide/src'
        }
    }
    examples
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
    // Xenon dependencies. downloadJGlobus task (below) downloads
    // xenon/lib/**/*.jar to ./lib/
    compile group: 'com.jcraft', name: 'jsch', version: jschVersion
    compile group: 'commons-net', name: 'commons-net', version: apacheCommonsNetVersion
    compile 'commons-httpclient:commons-httpclient:3.0.1'
    compile 'org.apache.jackrabbit:jackrabbit-webdav:2.10.1'
    compile 'joda-time:joda-time:2.8.1'
    compile group: 'com.google.code.findbugs', name: 'findbugs', version: '2.0.2'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.5'
    compile group: 'commons-httpclient', name: 'commons-httpclient', version: '3.0.1'
    compile group: 'org.apache.jackrabbit', name: 'jackrabbit-webdav', version: '2.10.1'
    compile group: 'joda-time', name: 'joda-time', version: '2.8.1'
    compile module(group: 'org.jglobus', name: 'cog-jglobus', version: cogJglobusVersion) {
        dependency group: 'org.jglobus', name: 'cog-jobmanager', version: cogJglobusVersion
        dependency group: 'org.jglobus', name: 'cog-url', version: cogJglobusVersion
        dependency group: 'commons-logging', name: 'commons-logging', version: '1.1'
        dependency group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.3', transitive: true
        dependency group: 'cryptix', name: 'cryptix-asn1'
        dependency group: 'cryptix', name: 'cryptix'
        dependency group: 'cryptix', name: 'cryptix32'
        dependency group: 'org.bountycastle', name: 'jce-jdk13', version: '131'
        dependency group: 'org.ietf', name: 'jgss'
        dependency group: 'com.claymoresystems', name: 'puretls', version: '0.9b4'
    }

    // Runtime dependencies
    runtime group: 'ch.qos.logback', name: 'logback-core', version: '1.0.11'
    runtime group: 'ch.qos.logback', name: 'logback-classic', version: '1.0.11'

    // Testing dependencies
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.9.5'
    integrationTestCompile group: 'junit', name: 'junit', version: '4.11'
    integrationTestCompile group: 'org.mockito', name: 'mockito-all', version: '1.9.5'

    userguideCompile sourceSets.main.output
    examplesCompile sourceSets.main.output
}

//     DOCUMENT
// ==============
javadoc {
    exclude 'nl/esciencecenter/xenon/engine/**', 'nl/esciencecenter/xenon/adaptors/**'

    // FIXME: remove me when all Javadoc warnings by Java 8 have been resolved
    failOnError = false
}

task javadocDevel(type: Javadoc) {
    description 'Generates Javadoc API documentation for the main source code for Xenon developers.'
    group 'Documentation'
    source = sourceSets.main.allJava
    classpath = sourceSets.main.compileClasspath
    title = javadoc.title + ' for Xenon developers'
    destinationDir = file("${project.docsDir}/javadoc-devel")
    options.showFromPrivate()

    // FIXME: remove me when all Javadoc warnings by Java 8 have been resolved
    failOnError = false
}


task adaptorDocumentation(type: JavaExec) {
    description 'Generate Markdown adaptor appendix'
    group 'documentation'
    main = 'nl.esciencecenter.xenon.doc.AdaptorDocGenerator'
    // TODO adaptors.md is made in wrong dir (build/), change builddir to build/docs/userguide, docs/userguide dir must be created before
    File output = file("${buildDir}/adaptors.md")
    args = [output]
    classpath = sourceSets.main.runtimeClasspath + sourceSets.userguide.runtimeClasspath
    outputs.file output
}

class ConcatFiles extends DefaultTask {
    @InputFiles
    FileCollection files
    @OutputFile
    File target
    @TaskAction
    void concat() {
        target.withWriter { writer ->
            files.each { file ->
                file.withReader { reader ->
                    writer << reader << '\n'
                }
            }
        }
    }
}

task userguideMarkdown(type: ConcatFiles, dependsOn: adaptorDocumentation) {
    description 'Concatenate userguide_src.md and adaptor appendix into userguid.md'
    group 'documentation'
    files = files('doc/userguide/userguide_src.md', "${buildDir}/adaptors.md")
    target = file('doc/userguide/userguide.md')
    inputs.files files
    outputs.file target
}

task checkPandocPresence(type:Exec) {
    executable 'pandoc'
    args = ['-v']
    standardOutput = new ByteArrayOutputStream()
}

task userguidePdf(type:Exec, dependsOn: [checkPandocPresence, userguideMarkdown]) {
    description 'Convert markdown to pdf (requires pandoc and texlive)'
    group 'documentation'
    workingDir 'doc/userguide'
    executable 'pandoc'
    File output = file('doc/userguide/userguide.pdf')
    inputs.file userguideMarkdown.target
    outputs.file output
    args = [userguideMarkdown.target, '-o', output]
}

//     TEST
// ==============

//Create the task that runs the integration tests found from the
//configured source directory and uses the correct classpath.
task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    //Ensure that integration tests are run every time when you invoke this task
    outputs.upToDateWhen { false }
}

//Ensure that our unit tests are run before our integration tests
integrationTest.mustRunAfter test

//Ensure that the HTML reports of unit and integration tests are written to different directories.
tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
    reports.junitXml.destination = file("${project.buildDir}/${name}-results")
}

//    PACKAGE
// ==============
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task examplesJar(type: Jar) {
    classifier = 'examples'
    from sourceSets.examples.output
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

// TODO Distributions:
// 1. for maven, xenon.jar, xenon-src.jar, xenon-javadoc.jar
// 2. minimal, xenon.jar + deps*.jar
// 3. full, xenon.jar + deps*.jar + javadoc + examples + javadocdevel + unit tests +  integration tests

distributions {
    main {
        contents {
            def jar = project.tasks[JavaPlugin.JAR_TASK_NAME]
            into("lib") {
                from(jar)
                from(project.configurations.runtime)
            }
            into("docs") {
                from(userguideMarkdown.target)
                from('doc/userguide/userguide.pdf')
                into("javadoc") {
                    from(javadoc.destinationDir)
                }
                from(sourcesJar)
            }
            from {
                ['README.md',
                'LICENSE',
                'NOTICE',
                'notices'
                ]
            }
            into('examples') {
                from('src/examples')
                // TODO would like to have example source code in examples/src/main/java, but it is in examples/java, rename did not work
                from(examplesJar)
            }
        }
    }
}
distZip.dependsOn javadoc, userguidePdf
distTar.dependsOn javadoc, userguidePdf
installDist.dependsOn javadoc, userguidePdf

//     PUBLISH
// ==============
publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId 'nl.esciencecenter.xenon'
            artifactId 'xenon'
            version version

            artifact sourcesJar
            artifact javadocJar
        }
    }
}

task wrapper(type: Wrapper) {
    description 'Generates gradle wrapper'
    group 'Build Setup'
    gradleVersion = '2.7'
}

//     DOCKER
// ==============

task checkDockerComposePresence(type: Exec) {
    executable 'docker-compose'
    args = ['-v']
    standardOutput = new ByteArrayOutputStream()
}

class DockerCompose extends Exec {
    def DockerCompose() {
        workingDir 'src/integrationTest/docker'
        executable 'docker-compose'
        dependsOn project.tasks.checkDockerComposePresence
    }
}

task dockerKill(type: DockerCompose) {
    args = ['kill']
}

task dockerCleanup(type: DockerCompose, dependsOn: dockerKill) {
    args = ['rm', '-f']
}

task dockerTest(type: DockerCompose) {
    args = ['run', '--rm', 'xenon-test']
}
dockerTest.finalizedBy dockerCleanup
check.dependsOn dockerTest
