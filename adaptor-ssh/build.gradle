apply plugin: 'org.unbroken-dome.test-sets'
apply plugin: 'com.github.johnrengelman.plugin-shadow'

testSets {
    integrationTest
    fixedClientEnvironmentTest
}

integrationTest.description = 'Run the integration tests against docker containers'
fixedClientEnvironmentTest.description = 'Run the integration tests within and against docker containers, should be run within `nlesc/xenon-fixed-client` docker container'

dependencies {
    compileOnly project(':core')

    compile group: 'com.github.NLeSC.mina-sshd', name: 'sshd-core', version: 'known-host-hash-with-port-1.6.0-SNAPSHOT'
    compile group: 'org.apache.mina', name: 'mina-core', version: '2.0.7'
    compile group: 'net.i2p.crypto', name: 'eddsa', version: '0.2.0'
    compile group: 'com.google.guava', name: 'guava', version: '19.0'

    testCompile project(':core')
    testCompile group: 'com.github.NLeSC.mina-sshd', name: 'sshd-core', version: 'known-host-hash-with-port-1.6.0-SNAPSHOT'
    testCompile group: 'org.apache.mina', name: 'mina-core', version: '2.0.7'
    testCompile group: 'net.i2p.crypto', name: 'eddsa', version: '0.2.0'
    testCompile group: 'com.google.guava', name: 'guava', version: '19.0'

    integrationTestCompile group: 'junit', name: 'junit', version: '4.12'
    integrationTestCompile project(':test-utils')

    fixedClientEnvironmentTestCompile group: 'junit', name: 'junit', version: '4.12'
    fixedClientEnvironmentTestCompile project(':test-utils')
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test

jar {
    manifest {
        attributes("Xenon-Adaptors": "nl.esciencecenter.xenon.adaptors.filesystems.sftp.SftpFileAdaptor, nl.esciencecenter.xenon.adaptors.schedulers.ssh.SshSchedulerAdaptor")
    }
}

task jacocoIntegrationTestReport(type: JacocoReport) {
    description 'Generate coverage report of integration tests'
    group 'Code coverage reporting'
    executionData integrationTest
    sourceSets sourceSets.main
    reports {
        xml.enabled = true // codecov depends on xml format report
    }
}

task jacocoFixedClientEnvironmentTestReport(type: JacocoReport) {
    description 'Generate coverage report of fixed client environment tests'
    group 'Code coverage reporting'
    executionData fixedClientEnvironmentTest
    sourceSets sourceSets.main
    reports {
        xml.enabled = true // codecov depends on xml format report
    }
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            groupId 'nl.esciencecenter.xenon.adaptor'
            artifactId 'xenon-adaptor-ssh'
            version version
            artifact shadowJar
            artifact javadocJar
            artifact sourcesJar
        }
    }
}
