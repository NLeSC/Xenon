// Depends on plugins
//    id 'java'
//    id 'distribution'
//    id 'maven-publish'

//    PACKAGE
// ==============
task sourcesJar(type: Jar, dependsOn: classes) {
    description "Creates jar file of Java sources"
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    description "Creates jar file of javadoc"
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task testApiJar(type: Jar) {
    description "Creates jar with test api"
    baseName = 'xenon-testapi'
    from sourceSets.testApi.output
}

task docGeneratorJar(type: Jar) {
    description "Creates jar with doc generator"
    baseName = 'xenon-docgenerator'
    from sourceSets.docGenerator.output
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

// Distributions:
// 1. for maven, xenon.jar, xenon-sources.jar, xenon-javadoc.jar, published on JCenter
// 2. tarball, xenon.jar + deps*.jar + javadoc, published on Github release

distributions {
    main {
        contents {
            def jar = project.tasks[JavaPlugin.JAR_TASK_NAME]
            into("lib") {
                from(jar)
                from(project.configurations.runtime)
            }
            into("docs") {
                into("javadoc") {
                    from(javadoc.destinationDir)
                }
                from(sourcesJar)
                from('doc')
            }
            from {
                ['README.md',
                'LICENSE',
                'NOTICE',
                'notices'
                ]
            }
        }
    }
}

distZip.dependsOn javadoc
distTar.dependsOn javadoc
installDist.dependsOn javadoc

//     PUBLISH
// ==============
publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId 'nl.esciencecenter.xenon'
            artifactId 'xenon'
            version version

            artifact sourcesJar
            artifact javadocJar
        }
        TestApiPublication(MavenPublication) {
            groupId 'nl.esciencecenter.xenon'
            artifactId 'xenon-testapi'
            version version

            artifact testApiJar
        }
        DocGeneratorPublication(MavenPublication) {
            groupId 'nl.esciencecenter.xenon'
            artifactId 'xenon-docgenerator'
            version version

            artifact docGeneratorJar
        }
        LiveTestPublication(MavenPublication) {
            groupId 'nl.esciencecenter.xenon'
            artifactId 'xenon-livetest'
            version version

            artifact liveTestJar
        }
    }
}
