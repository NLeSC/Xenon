// Depends on plugins
//    id 'java'
//    id 'distribution'
//    id 'maven-publish'

//    PACKAGE
// ==============
task sourcesJar(type: Jar, dependsOn: classes) {
    description "Creates jar file of Java sources"
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    description "Creates jar file of javadoc"
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task testJar(type: Jar, dependsOn: classes) {
    classifier = 'tests'
    from sourceSets.test.output
}

task integrationTestsJar(type: Jar, dependsOn: classes) {
    classifier = 'integrationTests'
    from sourceSets.integrationTest.output
}

artifacts {
    archives sourcesJar
    archives javadocJar
    archives integrationTestsJar
}

// Distributions:
// 1. for maven, xenon.jar, xenon-sources.jar, xenon-javadoc.jar, published on JCenter
// 2. tarball, xenon.jar + deps*.jar + javadoc, published on Github release

distributions {
    main {
        contents {
            def jar = project.tasks[JavaPlugin.JAR_TASK_NAME]
            into("lib") {
                from(jar)
                from(project.configurations.runtime)
            }
            into("docs") {
                into("javadoc") {
                    from(javadoc.destinationDir)
                }
                from(sourcesJar)
                from('doc')
            }
            from {
                ['README.md',
                'LICENSE',
                'NOTICE',
                'notices'
                ]
            }
        }
    }
}

distZip.dependsOn javadoc
distTar.dependsOn javadoc
installDist.dependsOn javadoc

//     PUBLISH
// ==============
publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId 'nl.esciencecenter.xenon'
            artifactId 'xenon'
            version version

            artifact sourcesJar
            artifact javadocJar
            artifact integrationTestsJar

        }
        TestPublication(MavenPublication) {
            from components.java
            groupId 'nl.esciencecenter.xenon'
            artifactId 'xenon-integrationTests'
            version version
            artifact integrationTestsJar
        }


    }
}
