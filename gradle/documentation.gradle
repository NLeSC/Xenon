// javadoc and userguide generation

javadoc {
    exclude 'nl/esciencecenter/xenon/adaptors/**'
    options.overview = file("${buildDir}/overview.html")
    options.showFromPublic()
    options.setNoTimestamp(true)
    options.addBooleanOption('html5', true)
}

task javadocDevel(type: Javadoc) {
    description 'Generates Javadoc API documentation for the main source code for Xenon developers.'
    group 'Documentation'
    source = sourceSets.main.allJava
    classpath = sourceSets.main.compileClasspath
    title = javadoc.title + ' for Xenon developers'
    destinationDir = file("${project.docsDir}/javadoc-devel")
    options.showFromPrivate()
    options.overview = file("${buildDir}/overview.html")
    options.setNoTimestamp(true)
    options.addBooleanOption('html5', true)
}

task adaptorDocumentation(type: JavaExec) {
    description 'Generates html snippet with options of adaptors (used in the javadoc overview page)'
    group 'documentation'
    main = 'nl.esciencecenter.xenon.utils.AdaptorDocGenerator'
    File output = file("${buildDir}/overview.html")
    args = [output]
    classpath = sourceSets.docGenerator.runtimeClasspath
    outputs.file output
}

javadoc.dependsOn adaptorDocumentation
javadocDevel.dependsOn adaptorDocumentation
